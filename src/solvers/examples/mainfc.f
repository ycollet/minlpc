C***********************************************************************
C FINITE-DIFFERENCING, CUSTOMIZED, NO BOUNDS
C***********************************************************************
C MAIN PROGRAM TO MINIMIZE A FUNCTION (REPRESENTED BY THE ROUTINE SFUN)
C OF N VARIABLES X - CUSTOMIZED VERSION, WITH FINITE DIFFERENCING
C
      DOUBLE PRECISION  X(50), F, G(50), W(700)
      DOUBLE PRECISION  ETA, ACCRCY, XTOL, STEPMX, FACCY, DSQRT
      COMMON /FINDIF/   ACCRCY
      EXTERNAL          SFUN
C
C SET UP FUNCTION AND VARIABLE INFORMATION
C N -  NUMBER OF VARIABLES
C X -  INITIAL ESTIMATE OF THE SOLUTION
C F -  ROUGH ESTIMATE OF FUNCTION VALUE AT SOLUTION
C LW - DECLARED LENGTH OF THE ARRAY W
C
      N  = 10
      DO 10 I = 1,N
         X(I) = I / FLOAT(N+1)
10    CONTINUE
      F  = 1.D0
      LW = 700
C
C SET UP CUSTOMIZING PARAMETERS
C ETA     - SEVERITY OF THE LINESEARCH
C MAXFUN  - MAXIMUM ALLOWABLE NUMBER OF FUNCTION EVALUATIONS
C XTOL    - DESIRED ACCURACY FOR THE SOLUTION X*
C STEPMX  - MAXIMUM ALLOWABLE STEP IN THE LINESEARCH
C FACCY   - ACCURACY OF COMPUTED FUNCTION VALUE
C ACCRCY  - ACCURACY OF COMPUTED FUNCTION AND GRADIENT VALUES
C           (VALUES ADJUSTED BECAUSE OF FINITE DIFFERENCING OF GRADIENT)
C MSGLVL  - DETERMINES QUANTITY OF PRINTED OUTPUT
C           0 = NONE, 1 = ONE LINE PER MAJOR ITERATION.
C MAXIT   - MAXIMUM NUMBER OF INNER ITERATIONS PER STEP
C
      MAXIT  = N/2
      MAXFUN = 150*N
      ETA    = .25D0
      STEPMX = 1.D1
      FACCY  = 1.D-15
      ACCRCY = DSQRT(FACCY)
      XTOL   = DSQRT(ACCRCY)
      MSGLVL = 1
C
C MINIMIZE THE FUNCTION
C
      CALL LMQN (IERROR, N, X, F, G, W, LW, SFUN,
     *     MSGLVL, MAXIT, MAXFUN, ETA, STEPMX, ACCRCY, XTOL)
C
C PRINT THE RESULTS
C
      IF (IERROR .NE. 0) WRITE(*,800) IERROR
      IF (MSGLVL .GE. 1) WRITE(*,810)
      IF (MSGLVL .GE. 1) WRITE(*,820) (I,X(I),I=1,N)
      STOP
800   FORMAT(//,' ERROR CODE =', I3,/)
810   FORMAT(10X, 'CURRENT SOLUTION IS ',/14X, 'I', 11X, 'X(I)')
820   FORMAT(10X, I5, 2X, 1PD22.15)
      END
C
C
      SUBROUTINE SFUN (N, X, F, G)
      DOUBLE PRECISION  X(N), G(N), F, T, H, XH, FH, HINV
      COMMON /FINDIF/   H
C
C ROUTINE TO COMPUTE FUNCTION (F) AND GRADIENT (G) OF THE OBJECTIVE
C FUNCTION AT THE POINT X
C GRADIENT OBTAINED VIA FINITE-DIFFERENCING
C FUNCTION OBTAINED FROM SUBROUTINE GETF
C
      HINV = 1.D0 / H
      CALL GETF (N, X, F)
      DO 10 I = 1,N
         XH   = X(I)
         X(I) = X(I) + H
         CALL GETF (N, X, FH)
         G(I) = (FH - F) * HINV
         X(I) = XH
10    CONTINUE
      RETURN
      END
C
C
      SUBROUTINE GETF (N, X, F)
      DOUBLE PRECISION  X(N), F, T
C
C ROUTINE TO EVALUATE FUNCTION (F)
C
      F = 0.D0
      DO 10 I = 1,N
         T    = X(I) - I
         F    = F + T*T
10    CONTINUE
      RETURN
      END
